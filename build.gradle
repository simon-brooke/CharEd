// Gradle build script for the Maud project

plugins {
    id 'application'
    id 'checkstyle'
}

ext.jme3Version = '3.6.1-stable' // current version of JMonkeyEngine

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

checkstyle {
    toolVersion '10.12.3'
}

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_20)) {
        // Suppress warnings that source value 8 is obsolete.
        options.compilerArgs << '-Xlint:-options'
    }
    options.compilerArgs << '-Xlint:unchecked'
    //options.deprecation = true // to provide detailed deprecation warnings
    options.encoding = 'UTF-8'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_1_10)) {
        options.release = 8
    }
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
Boolean isMacOS = DefaultNativePlatform.currentOperatingSystem.isMacOsX()

tasks.withType(JavaExec) { // Java runtime options:
    if (isMacOS) {
        jvmArgs '-XstartOnFirstThread'
    } else {
        //args '--forceDialog'
    }
    //args '--openGL3'
    //args '--openGL33'
    //args '--skipStartup'
    //args '--verbose' // to enable additional log output
    classpath sourceSets.main.runtimeClasspath
    enableAssertions true
    //jvmArgs '-verbose:gc'
    jvmArgs '-Xms4g', '-Xmx4g' // to enlarge the Java heap
    //jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

application {
    mainClass = 'maud.Maud'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of SNAPSHOTs
}
repositories {
    //mavenLocal() // to find local SNAPSHOTs of libraries
    mavenCentral()
    maven { url 'https://jitpack.io' } // to find jme3_xbuf_loader (see below)
    //maven { url 'https://s01.oss.sonatype.org/content/groups/staging' } // to find libraries staged but not yet released
    //maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' } // to find public SNAPSHOTs of libraries
}

dependencies {
    // from mavenCentral (or mavenLocal) repositories:
    implementation 'ch.qos.logback:logback-classic:1.4.7'
    runtimeOnly    'com.github.nifty-gui:nifty-style-black:1.4.3'
    implementation 'com.github.stephengold:Heart:8.7.0'
    implementation 'com.github.stephengold:Acorus:1.1.0'
    implementation 'com.github.stephengold:jme3-utilities-nifty:0.9.35'
    implementation 'com.github.stephengold:Minie:7.7.0'
    implementation 'com.github.stephengold:SkyControl:1.0.4'
    implementation 'com.github.stephengold:jme-ttf:3.0.0'
    implementation 'com.github.stephengold:Wes:0.7.5'
    runtimeOnly    'org.jmonkeyengine:jme3-awt-dialogs:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-blender:3.3.2-stable'
    runtimeOnly    'org.jmonkeyengine:jme3-desktop:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-lwjgl:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-plugins:' + jme3Version
    //runtimeOnly   'org.jmonkeyengine:jme3-testdata:3.1.0-stable'
    runtimeOnly    'org.openjdk.nashorn:nashorn-core:15.4'

    // from jitpack repositories:
    implementation 'com.github.xbuf.jme3_xbuf:jme3_xbuf_loader:0.9.1'
}

tasks.register('runForceDialog', JavaExec) {
    args '--forceDialog'
    mainClass = 'maud.Maud'
    description 'Runs the editor after displaying the Settings dialog.'
}

// cleanup tasks:

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs')

tasks.register('cleanDLLs', Delete) { // extracted Windows native libraries
    delete fileTree(dir: '.', include: '*.dll')
}
tasks.register('cleanDyLibs', Delete) { // extracted macOS native libraries
    delete fileTree(dir: '.', include: '*.dylib')
}
tasks.register('cleanLogs', Delete) { // JVM crash logs
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
tasks.register('cleanSandbox', Delete) { // Acorus sandbox
    delete 'Written Assets'
}
tasks.register('cleanSOs', Delete) { // extracted Linux and Android native libraries
    delete fileTree(dir: '.', include: '*.so')
}
